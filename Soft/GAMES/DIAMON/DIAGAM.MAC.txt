.TITLE	GAME_DIAMOND	; ┌─────────────────────────────────────────────────┐
.ENABL	MCL		; │	   Процесс игры программы DIAMONDS .	    │
.INCLU	/SY:P16MAC/	; └─────────────────────────────────────────────────┘
;────────────────────────────────────────────────────────────────────────────
;				КОНСТАНТЫ .
;────────────────────────────────────────────────────────────────────────────
MYSTACK		=	400		; Вершина стека .
MYREGISTER	=	176604		; Свой регистр .
REGIDENT	=	2		; Идентификатор регистрового вызова .
MYPRIORITET	=	57		; Приоритет .
NUMPRC		=	20.		; Кол-во одновременно работающих .
GAMEREGISTER	==	MYREGISTER	; Свой регистр .
SPTIK		=	50.		; Скорость вычитания бонусов .
SNTIME		=	50.		; Время примагниченности .
SPROT		=	2		; Скорость вращения шарика .
;────────────────────────────────────────────────────────────────────────────
;				ИНИЦИАЦИЯ ПРОЦЕССА .
;────────────────────────────────────────────────────────────────────────────
GINIT::
	CALL	SAVREG		; Сохранить регистры .
	MOV	#BUFMLT,R3	; Буфер многозадачности .
	MOV	#BEGPRC,R0	; Инициировать игру .
	CALL	PUTPRC		; ┘
	CALL	PUTPRC		; Сканировать клавиатуру .
	NEWROMP	#GAME.PROCESS	; Запустить процесс игры .
	MOV	R0,(PC)+	; ┐
GAMDES::			; Запомнить свой дескриптор .
	.WORD	0		; ┘
	CALL	INIMUS
	RETURN

BEGPRC:	.WORD	1,INIGAME,0	; Инициация процесса .
;────────────────────────────────────────────────────────────────────────────
;				ПРОЦЕСС ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
GAME.PROCESS:
	MOV	#MYSTACK,SP	; Своя область стека .
	UNPREG	#MYREGISTER	; Освободить свой регистр .
	PROREG	#BLKREGISTER	; Захватить свой регистр .
	SETPRI	#MYPRIO		; Выставить свой приоритет .
1$:	CLR	R0		; При вызове по времени .
	MOV	#1,R3		; Длительность задержки .
	WAITINT	#M.TIO		; Дождаться его .
	TST	R0		; Вызов по времени ?
	BEQ	2$		; Да .
	CALL	REGCALL		; Обработать регистр .
	BR	1$		; Повторить .
2$:	CALL	TIMCALL		; Подпрограмы по регистру или таймеру .
	BR	1$		; Повторить .
;────────────────────────────────────────────────────────────────────────────
;				ВЫЗОВ ПО РЕГИСТРУ .
;────────────────────────────────────────────────────────────────────────────
REGCALL::
	MOV	@#GAMEREGIST,R0	; Взять код запроса .
	JMP	@1$(R0)		; Исполнить запрос .
				; Таблица запросов .
1$:	.WORD	GAMEOFF,GAMEON,GAME.0
;────────────────────────────────────────────────────────────────────────────
;				ВЫКЛЮЧЕНИЕ ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
GAMEOFF:
	MOV	#BUFMLT,R0	; ┐
	MOV	#NUMPRC*3,R1	; Убить все процессы .
1$:	CLR	(R0)+		; │
	SOB	R1,1$		; ┘
	WAITINT	#0		; Дождаться обращения к регистру .
	JMP	REGCAL		; Исполнить .
;────────────────────────────────────────────────────────────────────────────
;				ВКЛЮЧЕНИЕ ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
GAMEON:
	MOV	#1$,R0		; ┐
	CALL	PUTPRC		; Перевывести лабиринт .
	CALL	PUTPRC		; Запустить сканирование .
	RETURN

1$:	.WORD	1,SCANKEY,0	; Сканирование клавиатуры .
	.WORD	1,OUTLAB,0	; Запуск лабиринта .
;────────────────────────────────────────────────────────────────────────────
;				ВКЛЮЧЕНИЕ ИГРЫ С #0 .
;────────────────────────────────────────────────────────────────────────────
GAME.0:
	MOV	#1$,R0		; ┐
	CALL	PUTPRC		; Перевывести лабиринт .
	RETURN

1$:	.WORD	1,INIGAM,0	; Запуск лабиринта .
;────────────────────────────────────────────────────────────────────────────
;				ВЫЗОВ ПО ВРЕМЕНИ .
;────────────────────────────────────────────────────────────────────────────
TIMCALL:
	MOV	#BUFMLT,R5	; Буфер многозадачности .
1$:	TST	(R5)		; Вызывать надо ?
	BEQ	2$		; Нет .
	DEC	(R5)+		; Пора пришла ?
	BNE	3$		; Нет .
	TST	(R5)		; Есть подпрограмма ?
	BEQ	3$		; Нет .
	CALL	@(R5)+		; Вызвать подпрограмму .
	TST	(R5)+		; Пропустить параметр .
	BR	4$		; Продолжить .
2$:	TST	(R5)+		; +2 .
3$:	CMP	(R5)+,(R5)+	; +4 .
4$:	CMP	R5,#BUFMLT+<NUMPRC*6>; Дошли до конца буфера ?
	BLO	1$		; Нет .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ИНИЦИАЦИЯ ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
INIGAME:
	MOV	#BUFMLT,R0	; ┐
	MOV	#NUMPRC*3,R1	; Очистить буфер многозадачности .
1$:	CLR	(R0)+		; │
	SOB	R1,1$		; ┘
	MOV	#2$,R0		; ┐
	CALL	PUTPRC		; Запустить сканирование клавиатуры .
	CALL	PUTPRC		; Запустить вывод лабиринта .
	CLR	NUMLAB		; 0 лабиринт .
	CLR	NUM.OCH		; 0 очков .
	MOV	#5,NUM.POP	; 5 попыток .
	RETURN

2$:	.WORD	1,SCANKEY,0	; Сканирование клавиатуры .
	.WORD	2,INILAB,0	; Инициализация лабиринта #0 .
;────────────────────────────────────────────────────────────────────────────
;			СКАНИРОВАНИЕ КЛАВИАТУРЫ .
;────────────────────────────────────────────────────────────────────────────
SCANKEY:
	MOV	#120,@#KBDCSR	; Команда клавиатуре .
	NOP			; ┘
	MOV	#8.,R1		; Кол-во байт на чтение .
	MOV	#KEYBUFER,R2	; Буфер для приёма .
1$:	MOVB	@#KBDBUF,(R2)+	; Взять код .
	NOP			; ┘
	BEQ	2$		; Если код пуст .
	CLR	ZAPOR		; Освободить запор .
2$:	SOB	R1,1$		; Повторить для всех кодов .
	INC	-4(R5)		; Снова вызвать в след. тике .
	BITB	#100,KEYBUFER+4	; Нажат НР ?
	BEQ	4$		; Нет .
	MOV	#BUFMLT,R0	; ┐
	MOV	#NUMPRC*3,R1	; Убить все задачи .
3$:	CLR	(R0)+		; │
	SOB	R1,3$		; ┘
	MOV	#5$,R0		; ┐
	CALL	PUTPRC		; Запустить сканирование клавиатуры .
	CALL	PUTPRC		; Запустить лабиринт .
	ADD	#2,NUMLAB	; След. номер .
7$:	MOV	#120,@#KBDCSR	; Команда клавиатуре .
	NOP			; ┘
	MOV	#KEYBUFER,R0	; ┐
	MOV	#8.,R1		; │
6$:	MOVB	@#KBDBUF,(R0)+	; │
	NOP			; Дождаться отжатия кнопки .
	SOB	R1,6$		; │
	TSTB	KEYBUFER+4	; │
	BNE	7$		; ┘
4$:	RETURN

5$:	.WORD	1,SCANKEY,0	; Сканирование клавиатуры .
	.WORD	2,INILAB,0	; Инициализация лабиринта #0 .
;────────────────────────────────────────────────────────────────────────────
;			ИНИЦИАЛИЗАЦИЯ ЛАБИРИНТА .
;────────────────────────────────────────────────────────────────────────────
INILAB:
	CLR	-2(R5)		; Эту подпрограмму больше не вызывать .
	MOV	NUMLAB,R0	; Номер лабиринта .
	MOV	R0,R1		; ┐
	ASR	R1		; Записать номер лабиринта для вывода .
	INC	R1		; │
	MOV	R1,NUM.LAB	; ┘
	MOV	#2,SPEED	; Нормальная скорость движения .
	MOV	SPEED,BALLD	; Расписать направление и скорость шарика .
	CLR	BALLK		; ┘
	CLR	FLKEY		; Ключа нет .
	MOV	2$(R0),R0	; Взять адрес лабиринта .
	MOV	(R0),BEGX	; ┐
	MOV	(R0)+,BALLX	; Координаты шарика .
	MOV	(R0),BEGY	; │
	MOV	(R0)+,BALLY	; ┘
	MOV	(R0),BEGC	; ┐
	MOV	(R0)+,COLOR	; Расписать начальный цвет шарика .
	MOV	(R0),BEGB	; ┐
	MOV	(R0)+,NUM.BON	; Расписать BONUS .
	MOV	#BUFLAB,R1	; Буфер под текущий лабиринт .
	MOV	#13.*12./2,R2	; ┐
1$:	MOV	(R0)+,(R1)+	; Записать лабиринт в буфер .
	SOB	R2,1$		; ┘
	MOV	#3$,R0		; ┐
	CALL	PUTPRC		; Запустить отрисовку лабиринта .
	RETURN
				; Список лабиринтов .
2$:	.WORD	 LAB1, LAB2, LAB3, LAB4, LAB5, LAB6, LAB7, LAB8, LAB9,LAB10
	.WORD	LAB11,LAB12,LAB13,LAB14,LAB15,LAB16,LAB17,LAB18,LAB19,LAB20
3$:	.WORD	1,OUTLAB,0	; Блок запуска вывода лабиринта .
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД ЛАБИРИНТА .
;────────────────────────────────────────────────────────────────────────────
OUTLAB:
	CALL	PALOFF		; Погасить палитру .
	CALL	BIGCIF		; Нарисовать большие цифры .
	MOV	R5,-(SP)	; ┐
	CALL	BORDER		; Нарисовать бордюры .
	MOV	(SP)+,R5	; ┘
	CALL	PALON		; Зажечь палитру .
	CALL	PALOFF		; Погасить палитру .
	CLR	-2(R5)		; Больше не вызывать .
	MOV	#BUFLAB,R0	; Буфер лабиринта .
	CLR	KCOUNT		; Очистить счетчик квадратов .
	CLR	DCOUNT		; Очистить счетчик брильянтов .
	CLR	FLDETH		; Пока живы .
	MOV	#2,SPEED	; Нормальная скорость движения .
	MOV	#4,KEYDOP	; Отменить доп. обработку .
	CLR	5$+2		; Y=0 .
1$:	CLR	5$		; X=0 .
2$:	MOVB	(R0)+,R1	; Взять очередной байт лабиринта .
	BEQ	4$		; Пустое место .
	CMP	R1,#RGY		; Это выбиваемый квадрат ?
	BHI	3$		; Нет .
	INC	KCOUNT		; Подсчитать .
3$:	CMP	R1,#DIA		; Это брильянт ?
	BNE	4$		; Нет .
	INC	DCOUNT		; Подсчитать .
4$:	MOV	TABSOO(R1),5$+4	; Взять адрес спрайта .
	MOV	R5,-(SP)	; Сохранить РОНы .
	MOV	R0,-(SP)	; ┘
	MOV	#5$,R0		; Адрес БП .
	CALL	$WORD		; Нарисовать .
	MOV	(SP)+,R0	; ┐
	MOV	(SP)+,R5	; Восстановить РОНы .
	INC	5$		; Сместиться по горизонтали .
	CMP	5$,#12.		; Конец строки ?
	BLO	2$		; Нет .
	INC	5$+2		; Сместиться по вертикали .
	CMP	5$+2,#13.	; Совсем конец ?
	BLO	1$		; Нет .
	CALL	OUTNLB		; Напечатать номер лабиринта .
	CALL	OUTOCH		; Напечатать кол-во очков .
	CALL	OUTPOP		; Кол-во попыток .
	CALL	OUTBON		; Кол-во бонусов .
	CALL	OUTKEY		; Ключ .
	MOV	#-4,ZAPOR	; Шарик ждёт своего часа .
	CALL	OUTSHAR		; Нарисовать шарик .
	MOV	#6$,R0		; ┐
	CALL	PUTPRC		; Запустить начало движения .
	CALL	PALON		; Поджечь палитру .
	RETURN

5$:	.WORD	0,0,0		; Буфер для обращения к процессу .
6$:	.WORD	3,BEGBALL,0	; Начало движения шарика .
;────────────────────────────────────────────────────────────────────────────
;			НАЧАЛЬНЫЙ ВЫВОД ШАРИКА .
;────────────────────────────────────────────────────────────────────────────
OUTSHAR:
	CALL	SAVREG		; Сохранить регистры .
	MOV	@#UR6,-(SP)	; Сохранить отображение .
	MOV	BALLX,1$	; Координаты .
	MOV	BALLY,1$+2	; ┘
	MOV	COLOR,1$+4	; Адрес спрайта .
	MOV	#1$,R0		; ┐
	CALL	$POINT		; Нарисовать .
	MOV	(SP)+,@#UR6	; Восстановить отображение .
	RETURN

1$:	.WORD	0,0,0		; Буфер для вывода спрайта .
;────────────────────────────────────────────────────────────────────────────
;			ВЫВОД НОМЕРА ЛАБИРИНТА .
;────────────────────────────────────────────────────────────────────────────
OUTNLB:
	MOV	#1$,R0		; Адрес блока для вывода .
	JMP	OUTCIF		; Вывести .

1$:	.WORD	NUM.LAB,2,6,TASCIF
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД ОЧКОВ .
;────────────────────────────────────────────────────────────────────────────
OUTOCH:
	MOV	#1$,R0		; Адрес блока для вывода .
	JMP	OUTCIF		; Вывести .

1$:	.WORD	NUM.OCH,6,28.,TASCIF
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД БОНУСОВ .
;────────────────────────────────────────────────────────────────────────────
OUTBON:
	MOV	#1$,R0		; Адрес блока для вывода .
	JMP	OUTCIF		; Вывести .

1$:	.WORD	NUM.BON,6,132.,TASCIF
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД ПОПЫТОК .
;────────────────────────────────────────────────────────────────────────────
OUTPOP:
	CALL	SAVREG		; Сохранить регистры .
	MOV	@#UR6,-(SP)	; Сохранить отображение .
	MOV	#72.*2,6$	; Горизонталь окна .
	MOV	#6,2$		; Ширина окна с попытками .
	MOV	NUM.POP,3$	; Кол-во попыток .
	BEQ	4$		; Если все вышли .
1$:	MOV	#6$,R0		; Вывести попытку .
	CALL	$POINT		; ┘
	ADD	#16.,6$		; Сместиться по горизонтали .
	DEC	(PC)+		; Окно заполнено ?
2$:	.WORD	0		; ┘
	BEQ	5$		; Да .
	DEC	(PC)+		; Попытки все показали ?
3$:	.WORD	0		; ┘
	BNE	1$		; Нет .
4$:	MOV	#6$,R0		; Стереть очередную попытку .
	CALL	$PCLEAR		; ┘
	DEC	2$		; Окно заполнено ?
	BNE	4$		; Нет .
5$:	MOV	(SP)+,@#UR6	; Восстановить отображение .
	RETURN
				; Блок для вывода спрайта (маски) .
6$:	.WORD	80.*2,<TASCIF-TASGAME>/4,BALLS+BALLRED
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД КЛЮЧА .
;────────────────────────────────────────────────────────────────────────────
OUTKEY:
	CALL	SAVREG		; Сохранить регистры .
	MOV	#189.,R0	; Горизонталь .
	MOV	#OKEY,R1	; Адрес спрайта ключа .
	MOV	#TASCIF+16.,R5	; Адрес ТАС .
	TST	FLKEY		; Ключ есть ?
	BNE	1$		; Да .
	MOV	#2$,R1		; Адрес пустого спрайта .
1$:	CALL	BIGSPR		; Нарисовать .
	RETURN

2$:	.WORD	5,12		; Пустое место вместо ключа .
	.BLKW	5*12
;────────────────────────────────────────────────────────────────────────────
;			УДАЛЕНИЕ ЗАДАЧИ ИЗ СПИСОКА ДИСПЕТЧЕРА .
;────────────────────────────────────────────────────────────────────────────
KILPRC:
	CALL	SAVREG		; Сохранить регистры .
	MOV	#BUFMLT,R1	; Буфер многозадачности .
	MOV	#NUMPRC,R2	; Кол-во задач .
1$:	CMP	2(R1),R0	; То что надо ?
	BEQ	2$		; Да .
	ADD	#6,R1		; След. задача .
	SOB	R2,1$		; Повторить для всех .
	RETURN

2$:	CLR	(R1)+		; Снять задачу .
	CLR	(R1)		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ЗАПИСЬ ЗАДАЧИ В СПИСОК ДИСПЕТЧЕРА .
;────────────────────────────────────────────────────────────────────────────
PUTPRC:
	CALL	SAVREG		; Сохранить регистры .
	MOV	#BUFMLT,R1	; Буфер диспетчера .
1$:	TST	2(R1)		; Задача свободна ?
	BEQ	2$		; Да .
	ADD	#6,R1		; След. задача .
	BR	1$		; Повторить .
2$:	MOV	(R0)+,(R1)+	; ┐
	MOV	(R0)+,(R1)+	; Поставить на исполнение .
	MOV	(R0)+,(R1)+	; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ОЖИДАНИЕ НАЖАТИЯ НА КЛАВИАТУРУ .
;────────────────────────────────────────────────────────────────────────────
BEGBALL:
	INC	-4(R5)		; Вызвать в следующий раз .
	TST	ZAPOR		; Запор уже открыт ?
	BNE	1$		; Нет .
	CLR	-4(R5)		; ┐
	CLR	-2(R5)		; Освободить процесс .
	MOV	#2$,R0		; ┐
	CALL	PUTPRC		; Загрузить движение шарика .
	CALL	PUTPRC		; ┘
	MOV	#SPTIK,BONTIK	; Загрузить скорость вычитания бонусов .
1$:	RETURN

2$:	.WORD	1,INKEY,0	; Нажатие на кнопки .
	.WORD	1,DWBALL,0	; Процесс движеия шарика .
;────────────────────────────────────────────────────────────────────────────
;				ДВИЖЕНИЕ ШАРИКА .
;────────────────────────────────────────────────────────────────────────────
DWBALL:
	TST	SNFLAG		; Шарик примагничен ?
	BEQ	2$		; Нет .
	DEC	SNCOUNT		; Уменьшить счетчик примагниченности .
	BPL	1$		; Если ещё на всё .
	CLR	SNFLAG		; Всё !
1$:	JMP	8$		; Шарик не выводить ; бонусы вычитать .
2$:	MOV	BALLX,10$+2	; Записать в старые координаты .
	MOV	BALLY,10$+4	; ┘
	CALL	POZCONTROL	; Контроль позиции .
	CALL	BAHCONTROL	; Контроль на втыкание .
	ADD	BALLD,BALLY	; Сместиться .
	ADD	BALLK,BALLX	; Сместиться .
	MOV	#10$,SPRSAVER	; ┐
	MOV	#PUTSPR,@#SPRREG; Стереть шарик .
	NOP			; ┘
	TST	FLDETH		; Умер ?
	BEQ	5$		; Нет .
	CLR	-2(R5)		; Больше не вызывать .
	MOV	#INKEY,R0	; ┐
	CALL	KILPRC		; Убить опрос клавиатуры .
	MOV	BEGB,NUM.BON	; ┐
	MOV	BEGC,COLOR	; Начальные данные .
	MOV	BEGX,BALLX	; │
	MOV	BEGY,BALLY	; ┘
	MOV	#2,SPEED	; ┐
	MOV	SPEED,BALLD	; Расписать начальные скорости .
	CLR	BALLK		; ┘
	MOV	#12$,R0		; Перезапуск игры .
	DEC	NUM.POP		; Ещё живы ?
	BMI	4$		; Нет .
	CALL	OUTPOP		; Вывести попытки .
3$:	MOV	#11$,R0		; Перевывод лабиринта .
4$:	JMP	PUTPRC		; Запустить вывод лабиринта .
5$:	TST	DCOUNT		; Прошли лабиринт ?
	BNE	6$		; Нет .
	CLR	-2(R5)		; Больше не вызывать .
	MOV	#INKEY,R0	; ┐
	CALL	KILPRC		; Убить опрос клавиатуры .
	ADD	#2,NUMLAB	; К следующему .
	MOV	#13$,R0		; ┐
	CALL	PUTPRC		; Запустить переход .
	MOV	#14$,R0		; Запустить подсчёт очков .
	JMP	PUTPRC		; ┘
6$:	MOV	#DOING,SPRSAV	; ┐
	MOV	#PUTSPR,@#SPRREG; Нарисовать шарик .
	NOP			; ┘
				; Кисть имеет суперцвет ?
	CMP	COLOR,#BALLS+BALLF1
	BLO	8$		; Нет .
	DEC	(PC)+		; Пора перевернуть ?
7$:	.WORD	0		; ┘
	BPL	8$		; Нет .
	MOV	#SPROT,7$	; Загрузить счетчик .
	ADD	#200,COLOR	; След. фаза суперцвета .
				; Была последняя фаза ?
	CMP	COLOR,#BALLS+BALLF8
	BLOS	8$		; Нет .
				; Начальная фаза .
	MOV	#BALLS+BALLF1,COLOR
8$:	DEC	BONTIK		; Пора вычитать бонусы ?
	BPL	9$		; Нет .
	MOV	#SPTIK,BONTIK	; Загрузить счетчик .
	TST	NUM.BON		; Бонусы кончились ?
	BEQ	9$		; Да .
	DEC	NUM.BON		; Уменьшить бонусы .
	CALL	OUTBON		; Нарисовать .
9$:	INC	-4(R5)		; Вызов через тик .
	RETURN

10$:	.WORD	$PCLEAR,0,0,BALLS+BALLMASKA
11$:	.WORD	10,OUTLAB,0
12$:	.WORD	10,THEEND,0
13$:	.WORD	10,INILAB,0
14$:	.WORD	7,PLUSBONUS,0
;────────────────────────────────────────────────────────────────────────────
;				КОНЕЦ ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
THEEND:
	CALL	PALOFF		; Погасить палитру .
	CALL	OUTPEN		; Нарисовать пень .
	CALL	PALON		; Зажечь палитру .
	CLR	-2(R5)		; Больше не вызывать .
	INC	ENDGAM		; Сообщить об окончании .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ПОДСЧЁТ ОЧКОВ .
;────────────────────────────────────────────────────────────────────────────
PLUSBONUS:
	DEC	NUM.BON		; Бонусы кончились ?
	BMI	1$		; Да .
	INC	NUM.OCH		; Увеличить очки .
	CALL	OUTBON		; Нарисовать бонусы
	CALL	OUTOCH		; Нарисовать очки .
	MOV	#S.BONUS,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	BR	PLUSBONUS	; Повторить .
1$:	CLR	-2(R5)		; Больше не вызывать .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ВВОД КЛАВИШИ .
;────────────────────────────────────────────────────────────────────────────
INKEY:
	CLR	BALLK		; Если клавиши не нажаты .
	BITB	#10,KEYBUFER+5	; → нажата ?
	BEQ	1$		; Нет .
	MOV	SPEED,BALLK	; Занести в направление .
1$:	BITB	#40,KEYBUFER+4	; ← нажата ?
	BEQ	2$		; Нет .
	MOV	SPEED,BALLK	; Занести в направление .
	NEG	BALLK		; ┘
2$:	ADD	KEYDOP,PC	; Нужно перевернуть ?
	NEG	BALLK		; Да .
	MOV	#4,-4(R5)	; Вызывать через 4 тика .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				КОНТРОЛЬ ВТЫКАНИЯ .
;────────────────────────────────────────────────────────────────────────────
BAHCONTROL:
	CALL	SAVREG		; Сохранить регистры .
1$:	MOV	BALLX,R2	; ┐
	ADD	BALLK,R2	; Расчитать предпологаемые координаты .
	MOV	BALLY,R3	; │
	ADD	BALLD,R3	; ┘
	MOV	#7$,R4		; Данные для контроля .
	MOV	#30,R5		; Длина блока данных .
2$:	MOV	R2,R0		; ┐
	ADD	(R4)+,R0	; │
	MOV	R3,R1		; │
	ADD	(R4)+,R1	; │
	ASH	#-4,R1		; │
	MOV	R1,WTYKY	; Расчёт места проверяемой точки в массиве .
	MUL	#12.,R1		; │
	ASH	#-5,R0		; │
	MOV	R0,WTYKX	; │
	ADD	R0,R1		; │
	ADD	#BUFLAB,R1	; ┘
	MOVB	(R1),R0		; Это пустое место ?
	BEQ	6$		; Да .
	CALL	@BUFCAL-2(R0)	; Исполнить подпрограмму по втыканию .
	MOV	SPEED,R0	; Скорость движения .
	TST	(R4)		; Что делать ?
	BEQ	4$		; Ничего .
	BPL	3$		; Прибавить .
	NEG	R0		; Отнять .
3$:	MOV	R0,@2(R4)	; Исполнить .
4$:	MOV	SPEED,R0	; Скорость движения .
	TST	4(R4)		; Что делать ?
	BEQ	1$		; Ничего .
	BPL	5$		; Прибавить .
	NEG	R0		; Отнять .
5$:	MOV	R0,@6(R4)	; Исполнить .
	CALL	POZCONTROL	; Проверить на границы .
	BR	1$		; Повторить всё снова .
6$:	ADD	#10,R4		; Сместиться ещё .
	SOB	R5,2$		; Повторить для всех данных .
	RETURN
7$:				; Данные для контроля втыканий .
;   Координаты	Переменная #1	Переменная #2 .
.WORD	0,10,	 1,BALLK,	 0,0		; ┌─╫─┐
.WORD	10,0,	 1,BALLD,	 0,0		; ╪   ╪
.WORD	17,10,	-1,BALLK,	 0,0		; └─╫─┘
.WORD	10,17,	-1,BALLD,	 0,0

.WORD	0,4,	 1,BALLD,	 1,BALLK	; ╔─┐
.WORD	4,0,	 1,BALLD,	 1,BALLK	; └─┘
.WORD	3,1,	 1,BALLD,	 1,BALLK
.WORD	1,3,	 1,BALLD,	 1,BALLK
.WORD	2,2,	 1,BALLD,	 1,BALLK

.WORD	13,0,	-1,BALLK,	 1,BALLD	; ┌─╗
.WORD	17,4,	-1,BALLK,	 1,BALLD	; └─┘
.WORD	14,1,	-1,BALLK,	 1,BALLD
.WORD	16,3,	-1,BALLK,	 1,BALLD
.WORD	15,2,	-1,BALLK,	 1,BALLD

.WORD	13,17,	-1,BALLK,	-1,BALLD	; ┌─┐
.WORD	17,13,	-1,BALLK,	-1,BALLD	; └─╝
.WORD	14,16,	-1,BALLK,	-1,BALLD
.WORD	16,14,	-1,BALLK,	-1,BALLD
.WORD	15,15,	-1,BALLK,	-1,BALLD

.WORD	0,13,	 1,BALLK,	-1,BALLD	; ┌─┐
.WORD	4,17,	 1,BALLK,	-1,BALLD	; ╚─┘
.WORD	3,16,	 1,BALLK,	-1,BALLD
.WORD	1,14,	 1,BALLK,	-1,BALLD
.WORD	2,15,	 1,BALLK,	-1,BALLD
;────────────────────────────────────────────────────────────────────────────
;			ЗАПУСК ТАЯНИЯ КВАДРАТА .
;────────────────────────────────────────────────────────────────────────────
TAJ:	MOV	#BUFTAJ,R0	; Буфер для таяния .
1$:	TST	(R0)		; Занято ?
	BEQ	2$		; Нет .
	ADD	#10,R0		; След место .
	BR	1$		; Повторить .
2$:	MOV	R0,3$+4		; Параметр для задачи .
	MOV	#$WCLEAR,(R0)+	; Подпрограмма очистки .
	MOV	WTYKX,(R0)+	; Координаты .
	MOV	WTYKY,(R0)+	; ┘
	MOV	#MASKI+MASKA1,(R0);Начальная фаза .
	MOV	#3$,R0		; Запустить процесс .
	CALL	PUTPRC		; ┘
	RETURN

3$:	.WORD	1,TAJANIE,0	; Блок запуска .
;────────────────────────────────────────────────────────────────────────────
;			ТАЯНИЕ КВАДРАТА .
;────────────────────────────────────────────────────────────────────────────
TAJANIE:
	MOV	#S.TRESK,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	MOV	(R5),R0		; Параметр = блок переменных .
	MOV	R0,SPRSAVER	; Адрес буфера .
	MOV	#PUTSPR,@#SPRREG; Захватить спрайт .
	NOP			; ┘
	SUB	#400,6(R0)	; Сместиться к след. фазе .
	CMP	6(R0),#MASKA5+MASKI;Кончилось ?
	BLO	1$		; Да .
	MOV	#2,-4(R5)	; Вызвать через 2 тика .
	RETURN

1$:	INC	-4(R5)		; Вызвать через тик .
	MOV	#2$,-2(R5)	; Подпрограмму завершения .
	RETURN

2$:	CLR	-2(R5)		; Конец цепочки .
	CLR	@(R5)		; Буфер свободен .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В КРАСНЫЙ КВАДРАТ .
;────────────────────────────────────────────────────────────────────────────
KRED:	JSR	R5,KVADOBS
	.WORD	BALLS+BALLRED,5,U.KRED,S.KRED
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В ЗЕЛЁНЫЙ КВАДРАТ .	
;────────────────────────────────────────────────────────────────────────────
KGREEN:	JSR	R5,KVADOBS
	.WORD	BALLS+BALLGREEN,4,U.KGREEN,S.KGREEN
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В ЖЁЛТЫЙ КВАДРАТ .	
;────────────────────────────────────────────────────────────────────────────
KYELLO:	JSR	R5,KVADOBS
	.WORD	BALLS+BALLYELLOW,3,U.KYELLOW,S.KYELLOW
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В СИНИЙ КВАДРАТ .	
;────────────────────────────────────────────────────────────────────────────
KBLUE:	JSR	R5,KVADOBS
	.WORD	BALLS+BALLBLUE,2,U.KBLUE,S.KBLUE
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В СЕРЫЙ КВАДРАТ .	
;────────────────────────────────────────────────────────────────────────────
KWAIT:	JSR	R5,KVADOBS
	.WORD	BALLS+BALLWAIT,1,U.KWAIT,S.KWAIT
;────────────────────────────────────────────────────────────────────────────
;			ОБЩАЯ ЧАСТЬ КВАДРАТОВ .
;────────────────────────────────────────────────────────────────────────────
KVADOB:
	TST	(SP)+		; Восстановить стек .
	MOV	6(R5),R0	; Звук если кубик не убит .
				; Это суперцвет ?
	CMP	COLOR,#BALLS+BALLF1
	BLO	1$		; Нет .
	TST	(R5)+		; Цвет не интересует .
	BR	2$		; Продолжить .
1$:	CMP	(R5)+,COLOR	; Шарик соответствующего цвета ?
	BNE	3$		; Нет .
2$:	ADD	(R5)+,NUM.OCH	; Прибавить к очкам .
	CALL	OUTOCH		; Нарисовать новые очки .
	CLRB	(R1)		; Освободить место в массиве .
	CALL	TAJ		; Поставить на погасание .
	DEC	KCOUNT		; Уменьшить счетчик квадратов .
	MOV	(R5)+,R0	; ┐
3$:	CALL	BEEPER		; Звук удара .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В КРАСНУЮ КИСТЬ .
;────────────────────────────────────────────────────────────────────────────
BRED:	JSR	R0,BRUSOBS
	.WORD	BALLS+BALLRED,S.BRED
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В ЗЕЛЁНУЮ КИСТЬ .	
;────────────────────────────────────────────────────────────────────────────
BGREEN:	JSR	R0,BRUSOBS
	.WORD	BALLS+BALLGREEN,S.BGREEN
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В ЖЁЛТУЮ КИСТЬ .	
;────────────────────────────────────────────────────────────────────────────
BYELLO:	JSR	R0,BRUSOBS
	.WORD	BALLS+BALLYELLOW,S.BYELLOW
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В СИНЮЮ КИСТЬ .	
;────────────────────────────────────────────────────────────────────────────
BBLUE:	JSR	R0,BRUSOBS
	.WORD	BALLS+BALLBLUE,S.BBLUE
;────────────────────────────────────────────────────────────────────────────
;			ВТЫКАНИЕ В СЕРУЮ КИСТЬ .	
;────────────────────────────────────────────────────────────────────────────
BWAIT:	JSR	R0,BRUSOBS
	.WORD	BALLS+BALLWAIT,S.BWAIT
;────────────────────────────────────────────────────────────────────────────
;			ОБЩАЯ ЧАСТЬ КИСТЕЙ .
;────────────────────────────────────────────────────────────────────────────
BRUSOBS:
	TST	(SP)+		; Восстановить стек .
	MOV	(R0)+,COLOR	; Сменить цвет шарика .
	MOV	(R0)+,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				СМЕРТЬ .
;────────────────────────────────────────────────────────────────────────────
$DETH:
	INC	(PC)+		; ┐
FLDETH:				; Выставить смертельный флаг .
	.WORD	0		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ИЗМЕНЕНИЕ КЛАВИШ .
;────────────────────────────────────────────────────────────────────────────
$KEYROT:
	MOV	#4,R0		; Изменить значение клавиш .
	XOR	R0,KEYDOP	; ┘
	MOV	#S.ROT,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				УМЕНЬШЕНИЕ СКОРОСТИ .
;────────────────────────────────────────────────────────────────────────────
$SMINUS:
	MOV	#1,R0		; Уменьшеная скорость .
	MOV	R0,SPEED	; ┘
	TST	BALLD		; Движение вниз ?
	BPL	1$		; Да .
	NEG	R0		; Значит '-' .
1$:	MOV	R0,BALLD	; Заприсать новую скорость и направление .
	MOV	#S.MINUS,R0	; Пикнуть .
	CALL	BEEPER		; ┘
mov	#balld,r0
halt
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				УВЕЛИЧЕНИЕ СКОРОСТИ .
;────────────────────────────────────────────────────────────────────────────
$SPLUS:
	MOV	#2,R0		; Увеличить скорость .
	MOV	R0,SPEED	; ┘
	TST	BALLD		; Движение вниз ?
	BPL	1$		; Да .
	NEG	R0		; Значит '-' .
1$:	MOV	R0,BALLD	; Заприсать новую скорость и направление .
	MOV	#S.PLUS,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				МАГНИТ .
;────────────────────────────────────────────────────────────────────────────
$SN:
	INC	(PC)+		; Шарик примагнитился !
SNFLAG:	.WORD	0		; ┘
	MOV	#SNTIME,SNCOUNT	; Загрузить счетчик .
	MOV	#S.SN,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ПОПЫТКА .
;────────────────────────────────────────────────────────────────────────────
$LIVE:
	INC	NUM.POP		; Увеличить кол-во попыток .
	CALL	OUTPOP		; Отобразить попытки .
	CLRB	(R1)		; Освободить место в массиве .
	CALL	TAJ		; Поставить на погасание .
	MOV	#S.ATT,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN	
;────────────────────────────────────────────────────────────────────────────
;				СУПЕР КИСТЬ .
;────────────────────────────────────────────────────────────────────────────
$SBRUSH:
				; Загрузить первый цвет .
	MOV	#BALLS+BALLF1,COLOR
	CLRB	(R1)		; ┐
	CALL	TAJ		; Удалить кубик .
	MOV	#S.SUPER,R0	; Пикнуть .
	CALL	BEEPER		; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				БРИЛЬЯНТ .
;────────────────────────────────────────────────────────────────────────────
$DIAMON:
	MOV	#S.DIAMON,R0	; Звук неудачи .
	TST	KCOUNT		; Все остальные выбиты ?
	BNE	1$		; Нет .
	CLRB	(R1)		; Освободить место в массиве .
	ADD	#10.,NUM.OCH	; Прибавить к очкам .
	CALL	OUTOCH		; Нарисовать новые очки .
	CALL	TAJ		; Поставить на погасание .
	DEC	DCOUNT		; Уменьшить счетчик брильянтов .
	MOV	#U.DIAMON,R0	; Звук удачи .
1$:	CALL	BEEPER		; Пикнуть .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				КЛЮЧ .
;────────────────────────────────────────────────────────────────────────────
$KEY:
	MOV	#S.KEY,R0	; Звук в случае неудачи .
				; Это суперцвет ?
	CMP	COLOR,#BALLS+BALLF1
	BHIS	1$		; Да .
				; Цвет жёлтый ?
	CMP	COLOR,#BALLS+BALLYELLOW
	BNE	2$		; Нет .
1$:	INC	FLKEY		; Признак ключа .
	CALL	OUTKEY		; Нарисовать ключ .
	CLRB	(R1)		; Стереть ключ .
	CALL	TAJ		; ┘
	MOV	#U.KEY,R0	; Звук удачи .
2$:	CALL	BEEPER		; Пикнуть .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ЗАМОК .
;────────────────────────────────────────────────────────────────────────────
$LOCK:
	MOV	#S.LOCK,R0	; В случае неудачи .
	TST	FLKEY		; Ключ есть ?
	BEQ	1$		; Нет .
	CLRB	(R1)		; Стереть замок .
	CALL	TAJ		; ┘
	CLR	FLKEY		; Ключа больше нет .
	CALL	OUTKEY		; ┘
	MOV	#U.LOCK,R0	; Звук удачи .
1$:	CALL	BEEPER		; Пикнуть .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				КОНТРОЛЬ ПОЗИЦИИ .
;────────────────────────────────────────────────────────────────────────────
POZCONTROL:
	MOV	BALLY,R0	; Вылезли за верх ?
	ADD	BALLD,R0	; ┘
	BPL	1$		; Нет .
	NEG	BALLD		; Изменить направление .
	MOV	#S.WALL,R0	; Звук удара .
	CALL	BEEPER		; ┘
	BR	2$		; Продолжить .
1$:	CMP	R0,#16.*12.	; Вылезли за низ ?
	BLOS	2$		; Нет .
	NEG	BALLD		; Изменить направление .
	MOV	#S.WALL,R0	; Звук удара .
	CALL	BEEPER		; ┘
2$:	MOV	BALLX,R0	; Вылезли влево ?
	ADD	BALLK,R0	; ┘
	BPL	3$		; Нет .
	NEG	BALLK		; Изменить направление .
	MOV	#S.WALL,R0	; Звук удара .
	CALL	BEEPER		; ┘
	BR	4$		; Продолжить .
3$:	CMP	R0,#32.*12.-16.	; Вылезли вправо ?
	BLOS	4$		; Нет .
	NEG	BALLK		; Изменить направление .
	MOV	#S.WALL,R0	; Звук удара .
	CALL	BEEPER		; ┘
4$:	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ПЕРЕМЕННЫЕ И КОНСТАНТЫ .
;────────────────────────────────────────────────────────────────────────────
BLKREGISTER:		; Блок описания регистра .
	.WORD	MYREGISTER,0,0,2000,REGIDENT,-1

		; Буфер диспетчера задач .
				; Структура :
				; 1 слово - время до вызова , если 0 то
BUFMLT:	.BLKW	NUMPRC.*3	; 			     не вызывать .
				; 2 слово - адрес задачи , если 0 то
				;			  задачи нет .
				; 3 слово - параметр для задачи .

NUM.LAB::	.WORD	0	; Номер лабиринта .
NUM.OCH::	.WORD	0	; Количество очков .
NUM.BON:	.WORD	0	; BONUS .
NUM.POP:	.WORD	4	; Попытки .
DOING:		.WORD	$POINT	; Подпрограмма .
BALLX:		.WORD	0	; Координаты шарика .
BALLY:		.WORD	0	; ┘
COLOR:		.WORD	0	; Адрес спрайта шарика .
BALLD:		.WORD	2	; Направление и скорость движения шарика .
BALLK:		.WORD	0	; То же по горизонтали .
ZAPOR:		.WORD	0	; Запор .
KEYBUFER:	.BLKB	8.	; Буфер под коды клавиатуры .
SPEED:		.WORD	2	; Скорость движения .
BUFCALL:			; Список подпрограмм по втыканиям .
.WORD	KRED,KGREEN,KYELLOW,KBLUE,KWAIT,BRED,BGREEN,BYELLOW,BBLUE,BWAIT
.WORD	$DETH,$KEYROT,$SMINUS,$SPLUS,$SN,$LIVE,$SBRUSH,$DIAMON,$KEY,$LOCK
BUFLAB:		.BLKB	12.*13.	; Буфер текущего лабиринта .
BUFTAJ:		.BLKW	40	; Буфер для таяния .
WTYKY:		.WORD	0	; Координаты втыкания .
WTYKX:		.WORD	0	; ┘
BEGX:		.WORD	0	; Начальные координаты .
BEGY:		.WORD	0	; ┘
BEGC:		.WORD	0	; Начальный цвет .
BEGB:		.WORD	0	; Начальный бонус .
NUMLAB:		.WORD	0	; Номер лабиринта .
KCOUNT:		.WORD	0	; Счетчик квадратов .
DCOUNT:		.WORD	0	; Счетчик брильянтов .
FLKEY:		.WORD	0	; Признак наличия ключа .
BONTIK:		.WORD	0	; Счетчик для вычитания бонусов .
KEYDOP:		.WORD	0	; Указатель подпрограммы для обработки .
SNCOUNT:	.WORD	0	; Счетчик примагниченности .
.END

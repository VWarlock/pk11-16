.TITLE	Game_Diamonds		; Игра Diamonds
				; начат 22 декабря 1992 г.
				; Коломейцев Артём & Гречишкин Александр .
.ENABL MCL
.INCLUDE /SY:P16MAC/		; Константы и определения ВПО .
.INCLUDE /SY:WDC/		; Константы и определения оконной системы .
.INCLUDE /SY:CONST/		; Константы АСПЕКТА .

.GLOBL	STFNT

.MACRO	.COCS		
	MOV	#COM,@#COCS
	NOP
.ENDM

.MACRO	.BWORD	A,B
	.BYTE	A&377,A/400
	.BYTE	B&377,B/400
.ENDM
	COCO	 == 176220	; Регистр регистрации АНИМАТОРА .
	COCS	 == 176216	; Регистр запроса АНИМАТОРА .

	.ASECT			;
	.=0			;
	.WORD	"PC,0,0,-1	;
	.ASCIZ	/Diamonds/	;
	.=500			;
	.WORD	STRTPRG		;
	.=1000
	.PSECT	PROGRAMM,GBL	;

START:
	.EXIT			; Если по RUN .
	MOV	#1000,SP	; Установим границу стека .
	MOV	R0,DS		; Запомним свой дескриптор .
	SETPRI	#10		; Понизим приоритет .
	MOV	#COM,R2		; ┐
	CLR	(R2)+		; │
	CLR	(R2)+		; Регисртируемся .
	MOV	DS,(R2)+	; │
	MOV	#STRTPRG,(R2)	; │
	MOV	#COM,@#COCO	; │
	NOP			; ┘
	MOV	DS,R0		; ┐
	GETPAR	#URX+4		; │
	MOV	#STFNT,URX+2	; Регистрируем шрифт .
	MOV	#2,URX		; │
	MOV	#URX,@#176224	; │
	NOP			; ┘
	JMP	ADSTSUB		; На подпрограммы .
;────────────────────────────────────────────────────────────────────────────
;			OПИСАНИЕ СТРУКТУРЫ ПРОГРАММЫ
;────────────────────────────────────────────────────────────────────────────
STRTPRG:
	ADFILATR,ADPRGATR,ADTBLSLD,ADSETUP,ADVARDES
ADPRGATR:								
;────────────────────────────────────────────────────────────────────────────
;		    ОПИСАНИЕ ФАЙЛОВЫХ АТРИБУТОВ ПРОГРАММЫ
;────────────────────────────────────────────────────────────────────────────
ADFILATR:
	ADBPFPRG		; Адр. описания файловых атрибутов
ADBPFPRG:
	.WORD	0		; Тип
	.ASCII	"Diamonds        "; Имя
	.ASCII	"GDIA"		; Пренадлежность
	.WORD	0		; Адрес первого кластера
	.WORD	0,0		; Длина файла в байтах
	.WORD	0,0		; Дата и время создания
	.WORD	10.,20.		; Координаты иконки
	.BLKB	144.		; Иконка
	.WORD	1		; Тип программы
	.WORD	STRTPRG		; Адрес блока параметров программы
	.WORD	0		; Контрольная сумма
	.WORD	ADSTSUB		; Адрес старта
	.BLKB	16.		; Имя под которым прогр. регистр. в впо
	.WORD	0		; Адрес описания оверл. сегментов
	.BLKW	4		; Резерв
        .BLKB	78.		; Коментарий
;────────────────────────────────────────────────────────────────────────────
;				ОПИСАНИЕ УСТАНОВОК .
;────────────────────────────────────────────────────────────────────────────
ADSETUP:						
	ADTBLSLD		;0  Адрес 
	ADS.MX:	0		;2  X
	ADS.MY:	0		;4  Y
	ADS.MB:	0		;6  Битовое поле
	ADS.EV:	0		;10 Могло произойти данное событие
	ADS.X::	0		;12 X в окне
	ADS.Y::	0		;14 Y в окне
	ADS.SL:	0		;16 Адрес слайда
	ADS.KD:	0		;20 Адрес кадра
	ADS.WKL			;22 Блочные пересылки при вкл.
	ADS.WYKL		;24 Блочные пересылки при выкл.
	KEYBOADR		;26 Адрес обработки клавиатуры .
	ADS.KEY: 0		;30 Код клавиши .
;────────────────────────────────────────────────────────────────────────────
ADS.WKL:
	.WORD	0,0		; Конец описания .
ADS.WYKL:
	.WORD	0,0		; Конец описания .
ADTBLSLD:
	.WORD	0		; Кол. задействованных слайдов .
	.BLKW	4.		; Всего может быть слайдов .
;────────────────────────────────────────────────────────────────────────────
;				СТАРТУЕМ .
;────────────────────────────────────────────────────────────────────────────
ADSTSUB:
	CALL	SINIT		; ┐
	CALL	INITWORK	; Инициализация .
WORKS:
	MOV	#WORKS,-(SP)	; Все будет по RTS PC .
	MOV	#0,COM		; ┐
	.COCS			; Основной цикл .
	MOV	#1000,SP	; Восстановим указатель стека .
	BR	WORKS		; Если несколько команд .
WRKS::	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ИНИЦИАЛИЗАЦИЯ .
;────────────────────────────────────────────────────────────────────────────
INITWORK:
	MOV	#2,COM		; ┐
	MOV	#SLMAIN,COM+2	; Установим стол .
	.COCS			; ┘
	MOV	#2,COM		; ┐
	MOV	#SLDIAMON,COM+2	; Откроем слайд игрового поля .
	.COCS			; ┘
	CALL	MOUSOFF		; Выключить мышь .
	CALL	OUTFACE		; Выводим все картиночки .
	CALL	GINIT		; Запустить игру .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ВЫБОР ПОДПРОГРАММЫ ПО МЕНЮ .
;────────────────────────────────────────────────────────────────────────────
WHATMENU::
	MOV	MENCON,R0	; Пункт .
	MOV	#100,PAUSE	; Это не пауза .
	JMP	@MENTBL(R0)	; Пойдем на обработку .
MENTBL:
	ABOUT			; 0  О игре .
	STGAME			; 2  Начать игру .
	PISKONOFF		; 4  Вкл/Выкл звуки .
	SOUNONOFF		; 6  Вкл/Выкл музыку .
	TABREC			; 10 Таблица рекордов .
;────────────────────────────────────────────────────────────────────────────
;				О ИГРЕ  ..
;────────────────────────────────────────────────────────────────────────────
PISKONOFF:
SOUNONOFF:
ABOUT:
	CALL	MENU		; Поднять название .
	CALL	PALOFF		; Погасить палитру .
	MOV	#TBLABO,R5	; Вывести выпуклый прямоугольник .
	CALL	OUTL		; ┘
	MOV	#ABT1,@#176224	; Вывести о игре .
	NOP			; ┘
	CALL	PALON		; Зажечь палитру .
	RETURN
;────────────────────────────────────────────────────────────────────────────
TBLABO:	.WORD	TASA,0,0,208.,240.,UPLIN,^B1110111011101110
	.WORD	TASA,5,10.,199.,220.,DOLIN,0
	.WORD	-1
;────────────────────────────────────────────────────────────────────────────
;				ТАБЛИЦА РЕКОРДОВ .
;────────────────────────────────────────────────────────────────────────────
TABREC:
	CALL	MENU		; Поднять название .
	CALL	PALOFF		; Погасить палитру .
	BR	3$		; Вывести таблицу пекордов .
1$:	CMP	NUM.OCH,@CONREC+22;Попали в таблицу рекордов ?
	BGT	2$		; Да .
	JMP	STG		; Нет, ну тогда на начало игры .
2$:	CALL	PALOFF		; Погасить палитру .
	CALL	SORT		; Отсортируем .
	MOV	R5,-(SP)	; Сохранить .
	CALL	3$		; Вывести таблицу рекордов .
	MOV	(SP)+,R5	; Восстановить .
	MOV	#100,KBD	; Теперь будем отрабатывать кнапки .
	JMP	INPNAM		; Приготовиться для ввода символов .	
3$:	CALL	RECORDS		; Вывести таблицу рекордов .
	MOV	#SPTOP10,R3	; Вывести название таблицы рекордов .
	CALL	PCTO		; ┘
	CALL	PALON		; Зажечь палитру .
	RETURN
TBR == 1$
;────────────────────────────────────────────────────────────────────────────
;			НАЧАЛО ВВОДА ИМЕНИ .
;────────────────────────────────────────────────────────────────────────────
INPNAM:
	MOV	(R5),R2		; Берем адрес имени с координатами .
	ADD	#10.,R2		; ┘
	MOV	#WRKSTR+6,R1	; Адрес буфера .
	MOVB	(R2)+,2(R1)	; ┐
	MOVB	(R2)+,3(R1)	; │
	MOVB	(R2)+,(R1)	; Копируем координаты .
	MOVB	(R2)+,1(R1)	; │
	MOV	#SR,R1		; ┘
	MOV	#12.,R3		; ┐
1$:	MOVB	#40,(R1)+	; Очистим буфер строки .
	SOB	R3,1$		; ┘
	MOV	#SR,R1		; Адрес начала буфера .
	CLR	WRKSTR+4	; Начальная позиция для вывода .
	CLR	R3		; Цвет строки .
	MOVB	-10(R2),R3	; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				НАЧАЛО ИГРЫ .
;────────────────────────────────────────────────────────────────────────────
STGAME:
	CALL	MENU		; Задвинуть название .
STG:	MOV	#4,@#GAMEREGI	; Запустить игру .
	NOP			; ┘
	CLR	PAUSE		; Будет пауза .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ОБРАБОТКА КЛАВИАТУРЫ .
;────────────────────────────────────────────────────────────────────────────
KEYBOARD:
	TST	ENDGAM		; Окончили игру ?
	BEQ	2$		; Нет .
	BMI	1$		; Уже спрашивали нажатия на кнопку .
	MOV	#-1,ENDGAM	; Один раз уже нажали .
	CLR	@#GAMEREGI	; Остановить игру .
	NOP			; ┘
1$:	CMPB	ADS.KEY,#40	; Нажали пробел ?
	BNE	6$		; Нет .
	CLR	ENDGAM		; Игра может начаться .
	JMP	TBR		; Если надо, то вывести таблицу рекордов .
2$:	TST	KBD		; Клавиатура нужна ?
	BEQ	6$		; Нет .
	MOV	ADS.KEY,R4	; Код клавиши .
	BIC	#^C377,R4	; Будем работать с байтами .
	CMPB	R4,#37		; Это забой ?
	BNE	5$		; Нет .
	DEC	WRKSTR+4	; Уменьшим номер вывода символа .
	BGE	3$		; Если больше .
	CLR	WRKSTR+4	; Номер вывода символа 0 .
3$:	CLR	WRKSTR+12	; ┐
	MOV	#100000,WRKSTR	; │
	MOV	#WRKSTR,@#176224; Стереть старую надпись .
	NOP			; ┘
	CMP	R1,#SR		; Можно забивать ?
	BLE	4$		; Можно, но на месте .
	DEC	R1		; Сместимся .
	DEC	R2		; ┘
4$:	MOVB	#40,(R1)	; ┐
	MOVB	#40,(R2)	; Всавим пробелы .
	RETURN			; ┘
5$:	TSTB	(R1)		; Можно еще символы ?
	BEQ	6$		; Нет .
	MOVB	R4,(R1)+	; ┐
	MOVB	R4,(R2)+	; Поставим символ .
	MOV	R3,WRKSTR+12	; ┐
	MOV	#100000,WRKSTR	; │
	MOV	#WRKSTR,@#176224; Напечатаем нужным цветом .
	NOP			; ┘
	INC	WRKSTR+4	; Увеличим номер вывода символа .
6$:	RETURN
;────────────────────────────────────────────────────────────────────────────
WRKSTR:	100000,1$,0,0,0,0,TASA	; 
1$:	.BYTE	2,33,200	; 
SR:	.ASCIZ	/            /	; 
	.EVEN			; 
KBD:	 .WORD	0		; 
ENDGAM:: .WORD	0		; 
PAUSE:	 .WORD	0		; 
;────────────────────────────────────────────────────────────────────────────
;				СОРТИРОВКА .
;────────────────────────────────────────────────────────────────────────────
SORT:
	CLR	R1		; Номер строки .
1$:	CMP	NUM.OCH,CONREC(R1); Мы набрали больше ?
	BGT	2$		; Да .
	TST	(R1)+		; Сместимся .
	BR	1$		; ┘
2$:	MOV	#TSSTR+22,R0	; Конец таблицы .
	ADD	#TSSTR,R1	; Наше положение в таблице .
3$:	CMP	R1,R0		; Дошли ?
	BEQ	6$		; Да .
	MOV	-(R0),R2	; Берем верхнюю строчку .
	MOV	2(R0),R3	; И текущую .
	ADD	#14.,R2		; Сместимся до имени .
	ADD	#14.,R3		; ┘
	MOV	#12.,R4		; ┐
4$:	MOVB	(R2)+,(R3)+	; Копируем имя .
	SOB	R4,4$		; ┘
	ADD	#5,R2		; Сместимся до номера лабиринта .
	ADD	#5,R3		; ┘
	MOVB	(R2)+,(R3)+	; ┐
	MOVB	(R2)+,(R3)+	; Копируем .
	ADD	#5,R2		; ┐
	ADD	#5,R3		; Сместимся до очков .
	MOV	#6.,R4		; ┐
5$:	MOVB	(R2)+,(R3)+	; Копируем .
	SOB	R4,5$		; ┘
	BR	3$		; Следующую строку .
6$:	MOV	R1,R5		; Сохраним для ввода имени .
	MOV	(R1),R1		; Встанем на позицию имени .
	ADD	#14.,R1		; ┘
	MOV	#12.,R4		; ┐
7$:	MOVB	#40,(R1)+	; Имя будет пробелами .
	SOB	R4,7$		; ┘
	ADD	#5,R1		; Стоим на номере лабиринта .
	MOV	NUM.LAB,R3	; Берем номер .
	MOV	#2,R4		; Количество цифр .
	CALL	CNVREC		; Преобразуем в строку символов .
	ADD	#7,R1		; Стоим на количестве очков .
	MOV	NUM.OCH,R3	; Берем количество очков .
	MOV	#6,R4		; Количество цифр .
	CALL	CNVREC		; Преобразуем в строку символов .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;		ПРЕОБРАЗОВАНИЕ ЦИФРЫ В СТРОКУ СИМВОЛОВ .
;────────────────────────────────────────────────────────────────────────────
CNVREC:
	CALL	SAVREG		; Сохраним регистры .
	ADD	R4,R1		; Сместимся на конец строки .
1$:	CLR	R2		; ┐
	DIV	#10.,R2		; Получим код цифры .
	ADD	#60,R3		; ┘
	MOVB	R3,-(R1)	; Пишем .
	MOV	R2,R3		; Остаток .
	SOB	R4,1$		; Повтрить для всех цифр .
	RETURN
;────────────────────────────────────────────────────────────────────────────
TSSTR:	.WORD	S1,S2,S3,S4,S5,S6,S7,S8,S9,S10,0
CONREC:	.WORD	10000.,9999.
	.BLKW	8.
;────────────────────────────────────────────────────────────────────────────
;				ОТОБРАЖЕНИЕ РЕКОРДОВ .
;────────────────────────────────────────────────────────────────────────────
RECORDS:
	MOV	#TBLREC,R5	; Вывести все прямоугольники .
	CALL	OUTL		; ┘
	MOV	#TBCIF,@#176224	; Вывести весть текст .
	NOP			; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
TBLREC:	.WORD	TASA,0,0,208.,240.,UPLIN,^B1110111011101110
	.WORD	TASA,5,46.,20.,186.,DOLIN,0
	.WORD	TASA,27.,46.,101.,186.,DOLIN,0
	.WORD	TASA,130.,46.,20.,186.,DOLIN,0
	.WORD	TASA,152.,46.,52.,186.,DOLIN,0
	.WORD	TASA,5,6,199.,36.,DOLIN,^B1110111011101110
	.WORD	-1
;────────────────────────────────────────────────────────────────────────────
MENU::
	TST	ENDGAM		; Окончили игру ?
	BNE	3$		; Нет .
	MOV	#33.,R1		; На сколько сдвигать .
	NEG	MENFLG		; В другую сторону .
	CALL	MOUSOFF		; Выключить мышь .
2$:	ADD	MENFLG,YN1	; ┐
	ADD	MENFLG,YN2	; Перемещаем .
	VWCRE	#WNAME+12	; │
	SOB	R1,2$		; ┘
	TST	MENFLG		; Включить мышь ?
	BMI	1$		; Нет .
	CLR	@#GAMEREGISTER	; Выключить задачу .
	NOP			; ┘
	CLR	KBD		; Выключить клавиатуру .	
	JMP	MOUSON		; Включить курсор .
1$:	TST	PAUSE		; Была пауза ?
	BNE	3$		; Нет .
	MOV	#2,@#GAMEREGI	; Продолжить игру .
	NOP			; ┘
	CLR	PAUSE		; Будет пауза .
3$:	CLR	KBD		; Выключить клавиатуру .	
	RETURN			; 
;────────────────────────────────────────────────────────────────────────────
;				ВЫВОД БОРДЮРОВ .
;────────────────────────────────────────────────────────────────────────────
OUTFACE:
	MOV	@#UR6,-(SP)	; Сохранить регистр отображения .
	MOV	#NAMTAS+4,R0	; Адрес ТАС .
	CLR	R1		; Номер цвета .
	MOV	#11.,R4		; Количество строк по 2 .
1$:	MOV	#2,R2		; Две строки за раз .
2$:	MOV	2(R0),@#UR6	; Берем регистр отображения .
	MOV	(R0)+,R3	; Адрес строки .
	ADD	#140000,R3	; Настроим .
	TST	(R0)+		; Сместимся на следующую строку .
	MOV	#104.,R5	; Всего в строке слов .
3$:	MOV	TBCOL(R1),(R3)+	; Заполняем .
	SOB	R5,3$		; ┘
	SOB	R2,2$		; Для 2 строк .
	TST	(R1)+		; Следующий цвет .
	SOB	R4,1$		; Для всего окна .
	CALL	OUTLINE		; Вывести все прямоугольники .
	CALL	OUTPICT		; Выводим картиночки названия .
	MOV	(SP)+,@#UR6 	; Восстановить регистр отображения .
	RETURN			; 
BORDER::
	MOV	@#UR6,-(SP)	; Сохранить регистр отображения .
	MOV	#-16.,BRD1	; ┐
	MOV	#-16.,BRD1+2	; │
	MOV	#-16.,BRD2	; Начальные координаты .
	MOV	#208.,BRD2+2	; ┘
	MOV	#26.,WRK	; Всего кубиков .
1$:	MOV	#BRD1,R0	; ┐
	CALL	$POINT		; Рисуем .
	ADD	#16.,BRD1	; Сдвигаемся вправо .
	MOV	#BRD2,R0	; ┐
	CALL	$POINT		; Рисуем .
	ADD	#16.,BRD2	; Сдвигаемся вправо .
	DEC	WRK		; Еще надо ?
	BGT	1$		; Да .
	MOV	#-16.,BRD1	; ┐
	CLR	BRD1+2		; │
	MOV	#384.,BRD2	; Начальные координаты .
	CLR	BRD2+2		; ┘
	MOV	#13.,WRK	; Всего кубиков .
2$:	MOV	#BRD1,R0	; ┐
	CALL	$POINT		; Рисуем .
	ADD	#16.,BRD1+2	; Сдвигаемся вниз .
	MOV	#BRD2,R0	; ┐
	CALL	$POINT		; Рисуем .
	ADD	#16.,BRD2+2	; Сдвигаемся вниз .
	DEC	WRK		; Еще надо ?
	BGT	2$		; Да .
	MOV	(SP)+,@#UR6 	; Восстановить регистр отображения .
	RETURN
;────────────────────────────────────────────────────────────────────────────
BRD1:	0,0,BORD+4		; Бордюры .
BRD2:	0,0,BORD+4		; ┘
WRK:	0			; Рабочая переменная .
;────────────────────────────────────────────────────────────────────────────
TBCOL:
	.WORD	^B0001000100010001
	.WORD	^B0010001000100010
	.WORD	^B0011001100110011
	.WORD	^B0100010001000100
	.WORD	^B0101010101010101
	.WORD	^B0110011001100110
	.WORD	^B0111011101110111
	.WORD	^B1000100010001000
	.WORD	^B1001100110011001
	.WORD	^B1010101010101010
	.WORD	^B1011101110111011
;────────────────────────────────────────────────────────────────────────────
;			НАРИСОВАТЬ КАРТИНКИ ВО ВСЕХ ОКНАХ .
;────────────────────────────────────────────────────────────────────────────
OUTPICT:
	MOV	#TBSPRITE,R3	; Таблица выводимых спрайтов .
1$:	MOV	(R3)+,R5	; Взять адрес ТАС .
	MOV	(R3)+,R0	; Взять координату Х .
	MOV	(R3)+,R1	; Взять адрес спрайта .
	MOV	R3,-(SP)	; ┐
	CALL	BIGSPR		; Вывести спрайт .
	MOV	(SP)+,R3	; ┘
	CMP	(R3),#-1	; Конец описаний ?
	BNE	1$		; Нет .
	RETURN			; 
PCTO == 1$
;────────────────────────────────────────────────────────────────────────────
SECTB	== 1$
;────────────────────────────────────────────────────────────────────────────
TBSPRITE: NAMTAS+12.,2,ASPNAM
	  NAMTAS+4.,58.,DIANAM
	  NAMTAS+12.,172.,ASPNAM
	  TASG+12.,2.,URSPR
	  TASG+12.,45.,OCHSPR
	  TASG+12.,91.,POPSPR
	  TASG+12.,149.,PRSPR
	  -1
;────────────────────────────────────────────────────────────────────────────
SPTOP10:  TASA+<13.*4>,75.,TOP
	  TASA+<13.*4>,117.,TEN
	  -1
;────────────────────────────────────────────────────────────────────────────
;			ВЫВОД ПРЯМОУГОЛЬНИКОВ .
;────────────────────────────────────────────────────────────────────────────
OUTLINE::
	MOV	#TBLIN,R5	; ┐
4$:	MOV	#7.,R1		; │
	MOV	#TBWRK,R2	; Скопировать данные в буфер .
1$:	MOV	(R5)+,(R2)+	; │
	SOB	R1,1$		; ┘
	MOV	R5,-(SP)	; Сохранить указатель таблицы бордюров .
	MOV	#TBWRK,R2	; Таблица описания прямоугольников .
	CALL	CREAREA		; Очистить область .
	DEC	6(R2)		; Абсолютное занчение по Х .
	SUB	#2,10(R2)	; Абсолютное занчение по У .
	MOV	12(R2),R0	; Получить адрес таблицы спрайтов .
	MOV	(R0),12(R2)	; Занести адрес левого верхнего угла .
	CALL	BYTSPR		; Вывести ЛВУ .
	MOV	6(R0),12(R2)	; Занести адрес левого нижнего угла .
	ADD	10(R2),4(R2)	; Получить координату ЛНУ .
	CALL	BYTSPR		; Вывести ЛНУ .
	MOV	6(R2),R3	; Длина полоски .
2$:	INC	2(R2)		; ┐
	MOV	12(R0),12(R2)	; Адрес спрайта нижней полоски .
	CALL	BYTSPR		; Выводим нижнюю полоску .
	SUB	10(R2),4(R2)	; На верхнюю полоску .
	MOV	10(R0),12(R2)	; Адрес спрайта верхней полоски .
	CALL	BYTSPR		; Выводим верхнюю полоску .
	ADD	10(R2),4(R2)	; На нижнюю полоску .
	SOB	R3,2$		; ┘
	MOV	4(R0),12(R2)	; Занести адрес ПНУ .
	CALL	BYTSPR		; Вывести ПНУ .
	MOV	2(R0),12(R2)	; Занести адрес ПВУ .
	SUB	10(R2),4(R2)	; Получить ПВУ .
	CALL	BYTSPR		; Вывести ПВУУ .
	MOV	10(R2),R3	; ┐
	SUB	#2,R3		; Высота полоски .
	ASR	R3		; ┘
3$:	ADD	#2,4(R2)	; ┐
	MOV	12(R0),12(R2)	; Адрес спрайта правой полоски .
	CALL	BYTSPR		; Выводим правую полоску .
	SUB	6(R2),2(R2)	; К левой полоске .
	MOV	10(R0),12(R2)	; Адрес спрайта левой полоски .
	CALL	BYTSPR		; Выводим левую полоску .
	ADD	6(R2),2(R2)	; К правой полоске .
	SOB	R3,3$		; ┘
	MOV	(SP)+,R5	; Восстановить указатель .
	CMP	(R5),#-1	; Еще есть бордюры ?
	BNE	4$		; Да .
	RETURN
OUTL	== 4$
;────────────────────────────────────────────────────────────────────────────
TBLIN:	.WORD	TASG,0,0,208.,36.,UPLIN,^B1110111011101110
	.WORD	TASG,5,9.,18.,22.,DOLIN,0
	.WORD	TASG,27.,9.,50.,22.,DOLIN,0
	.WORD	TASG,79.,9.,50.,22.,DOLIN,0
	.WORD	TASG,131.,9.,50.,22.,DOLIN,0
	.WORD	TASG,183.,9.,21.,22.,DOLIN,0
	.WORD	-1
TBWRK:	.BLKW	7.
;────────────────────────────────────────────────────────────────────────────
UPLIN::	BLUU,BRUU,BRDU,BLDU,BUL,BDL
DOLIN::	BLUD,BRUD,BRDD,BLDD,BDL,BUL
;────────────────────────────────────────────────────────────────────────────
;			ВЫВОД ОДНОГО БОРДЮРЧИКА  .
;────────────────────────────────────────────────────────────────────────────
BYTSPR:
	MOV	R3,-(SP)	; ┐
	MOV	R2,-(SP)	; Сохраним регистры .
	MOV	R0,-(SP)	; ┘
	MOV	(R2),R5		; Взять адрес ТАС .
	MOV	4(R2),R0	; ┐
	ASH	#2,R0		; Смещение по У .
	ADD	R0,R5		; ┘
	MOV	12(R2),R0	; Адрес спрайта .
	MOV	#2,R3		; Количество строк .
2$:	MOV	2(R5),@#UR6	; Настроитx регистр отображения .
	MOV	(R5)+,R1	; Взять адрес строки .
	ADD	#140000,R1	; ┘
	TST	(R5)+		; Сместиться на следующую .
	ADD	2(R2),R1	; Смещение по Х .
	MOVB	(R0)+,(R1)	; Выводим байтик .
	SOB	R3,2$		; ┘
	MOV	(SP)+,R0	; ┐
	MOV	(SP)+,R2	; Восстановим регистры .
	MOV	(SP)+,R3	; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ОЧИСТКА ОБЛАСТИ .
;────────────────────────────────────────────────────────────────────────────
CREAREA::
	MOV	(R2),R5		; Взять адрес ТАС .
	MOV	4(R2),R0	; ┐
	ASH	#2,R0		; Смещение по У .
	ADD	R0,R5		; ┘
	MOV	10(R2),R4	; Количество строк .
2$:	MOV	2(R5),-(SP)	; ┐
	BIC	#17,(SP)	; Настроит регистр отображения .
	MOV	(SP)+,@#UR6	; ┘
	MOV	(R5)+,R1	; Взять адрес строки .
	ADD	#140000,R1	; ┘
	TST	(R5)+		; Сместиться на следующую .
	ADD	2(R2),R1	; Смещение по Х .
	MOV	6(R2),R3	; Количество байт в строке .
1$:	MOVB	14(R2),(R1)+	; Прописываем одну строку по Х .
	SOB	R3,1$		; ┘
	SOB	R4,2$		; Прописываем все строки .
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ВКЛЮЧИТЬ МЫШЬ .
;────────────────────────────────────────────────────────────────────────────
MOUSOFF::
	MOV	#1,@#176214	; Включить курсор .
	NOP			; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			ВЫКЛЮЧИТЬ МЫШЬ .
;────────────────────────────────────────────────────────────────────────────
MOUSON::
	MOV	#2,@#176214	; Выключить курсор .
	NOP			; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;			СОХРАНЕНИЕ РЕГИСТРОВ .
;────────────────────────────────────────────────────────────────────────────
SAVREG::
	MOV	(SP)+,1$	; Адрес вызвавшей подпрограммы .
	MOV	R1,-(SP)	; ┐
	MOV	R2,-(SP)	; │
	MOV	R3,-(SP)	; Сохранить регистры .
	MOV	R4,-(SP)	; │
	MOV	R5,-(SP)	; ┘
	CALL	@(PC)+		; Исполнить вызвавшую подпрограмму .
1$:	.WORD	0		; ┘
	MOV	(SP)+,R5	; ┐
	MOV	(SP)+,R4	; │
	MOV	(SP)+,R3	; Восстановит регистры .
	MOV	(SP)+,R2	; │
	MOV	(SP)+,R1	; ┘
	RETURN
;────────────────────────────────────────────────────────────────────────────
;				ТАБЛИЦА РЕКОРДОВ .
;────────────────────────────────────────────────────────────────────────────
TBCIF:	.WORD	100000,S0,0,66.,14.,0.,TASA
S0:	.ASCII	<2><33><200>
S1:	.BYTE	11
	.BWORD	14.,66.
	.ASCII	<7><2>/01/
	.BYTE	11
	.BWORD	58.,66.
	.ASCII	/АЛЕКСАНДР   /
	.BYTE	11
	.BWORD	264.,66.
	.ASCII	/17/
	.BYTE	11
	.BWORD	308.,66.
	.ASCII	/010000/
S2:	.BYTE	11
	.BWORD	14.,84.
	.ASCII	<7><5>/02/
	.BYTE	11
	.BWORD	58.,84.
	.ASCII	/АРТЕМ       /
	.BYTE	11
	.BWORD	264.,84.
	.ASCII	/16/
	.BYTE	11
	.BWORD	308.,84.
	.ASCII	/009999/
S3:	.BYTE	11
	.BWORD	14.,102.
	.ASCII	<7><6>/03/
	.BYTE	11
	.BWORD	58.,102.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,102.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,102.
	.ASCII	/000000/
S4:	.BYTE	11
	.BWORD	14.,120.
	.ASCII	<7><12.>/04/
	.BYTE	11
	.BWORD	58.,120.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,120.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,120.
	.ASCII	/000000/
S5:	.BYTE	11
	.BWORD	14.,138.
	.ASCII	<7><12.>/05/
	.BYTE	11
	.BWORD	58.,138.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,138.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,138.
	.ASCII	/000000/
S6:	.BYTE	11
	.BWORD	14.,156.
	.ASCII	<7><12.>/06/
	.BYTE	11
	.BWORD	58.,156.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,156.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,156.
	.ASCII	/000000/
S7:	.BYTE	11
	.BWORD	14.,174.
	.ASCII	<7><12.>/07/
	.BYTE	11
	.BWORD	58.,174.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,174.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,174.
	.ASCII	/000000/
S8:	.BYTE	11
	.BWORD	14.,192.
	.ASCII	<7><12.>/08/
	.BYTE	11
	.BWORD	58.,192.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,192.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,192.
	.ASCII	/000000/
S9:	.BYTE	11
	.BWORD	14.,210.
	.ASCII	<7><12.>/09/
	.BYTE	11
	.BWORD	58.,210.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,210.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,210.
	.ASCII	/000000/
S10:	.BYTE	11
	.BWORD	14.,228.
	.ASCII	<7><12.>/10/
	.BYTE	11
	.BWORD	58.,228.
	.ASCII	/ПОКА ПУСТО  /
	.BYTE	11
	.BWORD	264.,228.
	.ASCII	/00/
	.BYTE	11
	.BWORD	308.,228.
	.ASCIZ	/000000/
	.EVEN
;────────────────────────────────────────────────────────────────────────────
;				ТЕКСТ О ИГРЕ .
;────────────────────────────────────────────────────────────────────────────
ABT1:	.WORD	100000,A1,0,32.,140.,0.,TASA
A1:	.BYTE	7,6
	.ASCII	/Игра "Diamonds"/
	.BYTE	11
	.BWORD	16.,48.
	.ASCIZ	<7><4>/┌┬┐╔╦╗╒╤╕╓╥╖├┼┤╠╬╣╞╪╡╟╫╢└┴┘╚╩╝╘╧╛╙╜╨╭╮╰╯/
	.EVEN
;────────────────────────────────────────────────────────────────────────────
ADVARDES:
MENFLG:		.WORD	-4		; Куда крутить окно с именем .
MENCON::	.WORD	0		; Номер пункта меню .
COM:		.BLKW	10.		; Буфер запроса к аниматору .
DS:		.WORD	0		; Свой дескриптор .
URX:		.BLKW	12.		; РО .

	.END	START


.include /wdc.mac/
.list
.asect
.=4
	.word	trrr, 1
	.word	trrr, 2
trrr:	mfps	2(sp)
	rti
.=110
	.asciz	/Operating system/
.=140
	.word	-2, 10000
.=200
	.word	"KB, t$tbl 	; 
.psect
;----------------------------------------
ROMBOOT:			;
	SETPRI	#3		;
	mov	#str,	r2	;
	mov	pc,	sp	;
	call	wname		;
;................................
	clr	r0		;
	mov	#20000,	r2	;
5$:				;
	add	50(r0),	r0	; romboot top
	add	#777,	r0	;
	bic	#777,	r0	;
	mov	#100000,r1	;
	add	r1,	r0	;
	sub	r2,	r0	;
10$:				;
	mov	-(r0),	-(r1)	;
	cmp	r2,	r1	;
	bne	10$		;
	mov	r2,	r0	;
	add	r2,	r2	;
	com	#0		;
	bne	5$		;
;................................
	NEWROM	#RSTINI		;
	NEWROM	#KBEINI		;
	NEWROM	#hdcini		;
;--------------------------------
	mov	#20040,	r2	;
	mov	#2,	r3	;
	call	creproc		;
	mov	#40040,	r2	;
	mov	#4,	r3	;
	call	creproc		;
;----------------------------------------
boot00:				; RT
	mov	#140,	r2	;
	mov	#14.,	r3	;
	call	creproc		;
	SETPRI	#1		;
;................................ 
	mov	#vdarea,r0	;
	ARCRE			;
	mov	vdarea+2,vdvw+4	;
	mov	#vdvw,	r0	;
	VWCRE			;
;................................
	mov	desc+p.ur0, ur3	;
;-------------------------------- READ	#60000(BUF), #256.(WCN), #0(BLK)
boot:				;
	tst	@#157130	; hard-disk ready ?
	beq	boot		;
	mov	#170$+3,r0	;
	mov	#177130,r1	; hdcsr
	movb	#10,	(r0)	; dev.num
15$:				;
;................................
	inc	(pc)+		;
17$:	.word	-1		;
	bne	20$		;
	movb	(r1),	(pc)+	;
18$:	.word	0		;
;................................
20$:				;
	cmpb	#12,	(r0)	;
	bne	40$		;
	movb	18$,	(r0)	;
	bpl	30$		;
	movb	(r1),	(r0)	;
	rorb	(r0)		;
	rorb	(r0)		;
	rorb	(r0)		;
30$:				;
	bic	#^c7*400,(r0)	;
;................................
40$:				;
	mov	#170$,	(r1)	;
	nop			;
50$:				;
	tstb	(r1)		;
	bpl	50$		;
	bit	#177400,(r1)	;
	beq	120$		;
	bitb	#10,	(r0)	;
	beq	boot		;
	incb	(r0)		;
	br	15$		;
;--------------------------------
120$:				;
	movb	#-1,	18$	;
	SETPRI	#-12345		;
	SETPRI	#1		; 
	br	boot00		;
;--------------------------------------------------
170$:	.word	0, 0, 60000, 400; area for read-disk request
desc:	.blkb	prolen		; area for descriptor
vdarea:	.word	0,	0,	12.,	40.
..xx=142400+360.
	.word	..xx,	vm1+pl0 ; 1
	.word	..xx,	vm1+pl0 ; 2
..x=142400
	.word	..x,	vm1+pl0 ; 3
..x=..x+40.
	.word	..x,	vm1+pl0 ; 4
..x=..x+40.
	.word	..x,	vm1+pl0 ; 5
	.word	..x,	vm1+pl0 ; 6
..x=..x+40.
	.word	..x,	vm1+pl0 ; 7
..x=..x+40.
	.word	..x,	vm1+pl0 ; 8
..x=..x+40.
	.word	..x,	vm1+pl0 ; 9
	.word	..x,	vm1+pl0 ;10
..x=..x+40. 
	.word	..x,	vm1+pl0 ;11

	.word	..xx,	vm1+pl0 ;12
vdvw:	.word	vwcre,0,0,0,0,8.,300,17.,30.,1001
;vdvw:	.word	vwcre,0,0,0,0,14.,300,17.,30.,1001
;--------------------------------------------------
creproc:			;
	mov	#desc+prolen,r0	;
	mov	#prolen/2,r1	;
10$:				;
	clr	-(r0)		;
	sob	r1,	10$	;
;................................
	add	#2,	(r2)	;
	beq	15$		;
	mov	#2,   desc+p.pri;
15$:				;
	mov	(r2)+,desc+p.pc ; PC+2	
	bne	16$		;
	mov	#120000,desc+p.mask	;m.irq+m.hlt
16$:				;
	mov	(r2), desc+p.sp ; SP
;................................
	add	#10-42,	r2	;
	call	wname		;
;................................
	GET4K	r3		;
	asr	r3		;
	mov	#desc+p.ur0, r1	;
30$:				;
	mov	r0,	(r1)+	;
	add	#40,	r0	;
	sob	r3,	30$	;
;--------------------------------
	mov	runnin,	r0	;
	mov	p.mem-p.sp(r0), r0
	mov	r0,	r1	;
	NEWPROC	#desc		;
	mov	maplen,	r2	;
40$:				;
	movb	#-1,	(r1)+	;
	sob	r2,	40$	;
	return			;
;--------------------------------
wname::
	mov	r0,	r1
	add	#p.name,r1
10$:
	movb	(r2)+,	(r1)+
	bne	10$
	return
str:	.asciz	/ROM-proc loader /
.end romboot
